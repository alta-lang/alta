import "libc/lib/stdio" as stdio
import String, rawstringLength from "string"
import rawstring, rawconststring, Size, uint8 from "types"
import numberToString, floatingPointToString, BucketList from "util"
import INT_MAX from "libc/lib/limits"
import Path from "fs"

export stdio.printf as printf

##if platform == "windows"
@@CTranspiler.include("Windows.h");

declare literal function SetConsoleCP(cp: unsigned int): int
declare literal function SetConsoleOutputCP(cp: unsigned int): int

declare literal var CP_UTF8: const unsigned int

let utf8Set = false
##end if

function ensureUTF8Output(): void {
##if platform == "windows"
  if !utf8Set {
    utf8Set = true
    SetConsoleCP(CP_UTF8)
    SetConsoleOutputCP(CP_UTF8)
  }
##end if
}

export enum Error: int {
  None = 0,
  Unknown = 1,
  PathNotFound = 2,
}

export enum Mode: int {
  Read = 1,
  Write = 2,
}

function translateMode(mode: Mode, overwrite: bool): ptr const byte {
  if mode & Mode.Read && mode & Mode.Write && overwrite {
    return "w+b"
  } else if mode & Mode.Write && overwrite {
    return "wb"
  } else if mode & Mode.Read && mode & Mode.Write {
    return "r+b" # not a+ because we want to be able to write anywhere
  } else if mode & Mode.Write {
    return "r+b" # we don't want to overwrite existing data, but we want to be able to write anywhere
  } else {
    return "rb"
  }
}

export class File {
  private var handle: ptr stdio.FILE = nullptr
  private var path = new Path
  private var mode: Mode = Mode.Read
  private var dtor: BucketList

  public constructor() {}
  public constructor(path: Path, mode: Mode = Mode.Read, overwrite: bool = false) {
    let res = this.open(path, mode, overwrite)
    assert res.isOk
  }

  public static function tryOpen(path: Path, mode: Mode = Mode.Read, overwrite: bool = false): Result<File, Error> {
    let file = new File
    let res = file.open(path, mode, overwrite)
    return res.map<File>((dummy: void) => File { return file })
  }

  public function open(path: Path, mode: Mode = Mode.Read, overwrite: bool = false): Result<void, Error> {
    # also assign it to a local variable so we can copy it into the lambda below
    let handle: ptr stdio.FILE = this.handle = stdio.fopen(path, translateMode(mode, overwrite))
    if !this.handle {
      return new Result<void, Error>(error: Error.Unknown)
    }

    this.path = path
    this.mode = mode

    this.dtor = new BucketList(@copy(handle) () => void {
      if stdio.fclose(handle) {
        # TODO: what should we do with close failure now that we don't have exceptions?
      }
    })

    return new Result<void, Error>(ok: void)
  }

  public function close(foo: String): void {
    if this.handle {
      # if ours was the last reference to that file, this will automatically close it
      # (by destroying the old BucketList)
      this.dtor = new BucketList
      this.handle = nullptr
    }
  }

  public function read(): byte? {
    let char = stdio.fgetc(this.handle)
    if char == stdio.EOF
      return null
    return char as byte
  }

  public function read(count: Size): String? {
    let str = new String(length: count)
    let readSome = false
    while count > INT_MAX {
      if !stdio.fgets(str, INT_MAX, this.handle) {
        if readSome {
          return str.substring(0, length: str.length)
        } else {
          return null
        }
      }
      count -= INT_MAX
      readSome = true
    }
    # `as int` isn't necessary, but it's used to silence a compiler warning
    if !stdio.fgets(str, count as int, this.handle) {
      if readSome {
        return str.substring(0, length: str.length)
      } else {
        return null
      }
    }
    return str.substring(0, length: str.length)
  }

  public @read function valid(): bool {
    return this.handle != nullptr
  }

  public @read function file(): ptr stdio.FILE {
    return this.handle
  }

  public @read function ended(): bool {
    return !!stdio.feof(this.handle)
  }

  public to ptr stdio.FILE {
    return this.handle
  }

  public to bool {
    return this.valid
  }
}

export type ConsoleData = String | double | int | char | bool

function toString(data: ConsoleData): String {
  if data instanceof String {
    return data as String
  } else if data instanceof double {
    return floatingPointToString<double>(data as double)
  } else if data instanceof char {
    let char = data as char
    return new String(&char, 1)
  } else if data instanceof int {
    return numberToString<int>(data as int)
  } else if data instanceof bool {
    return (data as bool) ? "true" : "false"
  }
  # this is literally impossible, but just in case...
  assert false
}

export function print(data: ConsoleData...): void {
  ensureUTF8Output()
  for i: Size in 0..data.length {
    stdio.printf("%s", toString(data[i]).data)
  }
}

export function printLine(data: ConsoleData...): void {
  for i: Size in 0..data.length {
    print(data[i])
  }
  stdio.printf("\n")
}

export function printLine(message: rawconststring): void {
  ensureUTF8Output()
  stdio.printf("%s\n", message)
}

export function printError(data: ConsoleData...): void {
  ensureUTF8Output()
  for i: Size in 0..data.length {
    stdio.fprintf(stdio.stderr, "%s", toString(data[i]).data)
  }
  stdio.fprintf(stdio.stderr, "\n")
}

export function printError(message: rawconststring): void {
  ensureUTF8Output()
  stdio.fprintf(stdio.stderr, "%s\n", message)
}

export function format(data: ConsoleData...): String {
  let result = new String
  for i: Size in 0..data.length {
    result += toString(data[i])
  }
  return result
}
